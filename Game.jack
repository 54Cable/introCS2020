class Game
{
    field Player player;
    field Map map;
    static int key_lag;
    constructor Game new(int stage_num)
    {
        do Stage.init(stage_num);
        do Targets.init();
        do Blocks.init();
        let player=Player.new(Stage._player_x(),Stage._player_y());
        let map=Map.new(Stage._map_img());
        let key_lag=100;
        return this;    
    }
    method Map _map()
    {
        return map;
    }
    method Player _player()
    {
        return player;
    }
    method void clear_scrn()
    {
        do player.clear_scrn();
        do Blocks.clear_scrn();
        return;
    }
    method void draw()
    {
        do Blocks.draw();
        do player.draw();
        do Targets.draw();
        return;
    }
    method void play()
    {
        var char inp;
        do map.draw();
        do Blocks.draw();
        while(true)
        {
            let inp=Keyboard.keyPressed();
            //L,U,R,D = 130~133
            if(inp=130)
            {
                do clear_scrn();
                do player.move(0,-1);
                do draw();
                do Sys.wait(key_lag);
            }
            if(inp=131)
            {
                do clear_scrn();
                do player.move(-1,0);
                do draw();
                do Sys.wait(key_lag);
            }
            if(inp=132)
            {
                do clear_scrn();
                do player.move(0,1);
                do draw(); 
                do Sys.wait(key_lag);
            }
            if(inp=133)
            {
                do clear_scrn();
                do player.move(1,0);
                do draw();
                do Sys.wait(key_lag);   
            }
        }
        return ;
    }
}